use org.dwcj.interfaces.EventProxy
use org.dwcj.bridge.IDwcjBBjBridge
use java.util.ArrayList

class public BBjEventProxy implements EventProxy

    field private Object obj!
    field private String method!

    method public BBjEventProxy(Object obj!, String method!)
        #obj! = obj!
        #method! = method!
    methodend

    method public void onEvent(BBjEvent ev!)

            rem DWCJ declares certain event callbacks as private to prevent them from
            rem being accessible by users of the API
            rem hence this bridge has to use reflection in order to invoke the event callbacks
            rem after making them accessible
            rem potential improvement: clarify the proxy tpyes to get the method directly and
            rem avoid iterating the whole method list

            o! = #obj!


            l!=  java.util.Arrays.asList(o!.getClass().getDeclaredMethods())
            it! = l!.iterator()
            while it!.hasNext()
                m! = it!.next()
                if m!.getName() = #method! then break
            wend

            m!.setAccessible(BBjAPI.TRUE)
            m!.invoke(o!,ev!)

            rem this was the original invocation (keeping for reference):
            rem eval("o!."+#method!+"(ev!)")
    methodend

classend


class public DwcjHelper implements IDwcjBBjBridge

    method public CustomObject getEventProxy(Object obj!, String method!)
        methodret new BBjEventProxy(obj!, method!)
    methodend
    
    method public CustomObject getEventProxy(Object obj!, String method!, String eventClass!)
        ep! = eval("new "+eventClass!+"(obj!, method!)")
        methodret ep!
    methodend
    
    method public int msgbox(String msg!, int options!, String title!)
        methodret msgbox(msg!,options!,title!)
    methodend 

    method public BBjControl createWidget(String class!, BBjWindow w!)
        wnd! = w!
        widget! = eval("new "+class!+"(wnd!)")
        methodret widget!
    methodend

    method public java.lang.Object createInstance(java.lang.String classname!)
        createString! = "new "+classname!+"()"
        the_instance! = eval(createString!)

        methodret the_instance!
    methodend

    method public java.lang.Object invokeMethod(java.lang.Object e!, java.lang.String method!, java.util.ArrayList args!)
        cmd! = "e!."+method!+"("
        if args!<>null() AND args!.size()>0 then
          for i=0 to args!.size()-1
              cmd! = cmd! + "args!.get("+str(i)+"),"
          next
          cmd! = cmd!.substring(0,cmd!.length()-1)
        fi
        cmd! = cmd! + ")"
        result!=eval(cmd!)
        methodret result!
    methodend

    method public ArrayList call(String pgm!, ArrayList args!)
        stmt$="CALL """+pgm!+""""
        prep$="ENTER"+$0a$
        for argnum=0 to args!.size()-1
            arg! = args!.get(argnum)
            if arg!.getType()=org.dwcj.bbj.BBjVar.BBjGenericType.NUMERIC then
                stmt$=stmt$+",A"+str(argnum)+""
                x$ = "A"+str(argnum)+" = args!.get("+str(argnum)+").getNumVal()"
            fi
            if arg!.getType() = org.dwcj.bbj.BBjVar.BBjGenericType.INTEGER then
                    stmt$=stmt$+",A"+str(argnum)+"%"
                    x$ = "A"+str(argnum)+"% = args!.get("+str(argnum)+").getIntVal()"
            fi
            if arg!.getType()= org.dwcj.bbj.BBjVar.BBjGenericType.STRING then
                    stmt$=stmt$+",A"+str(argnum)+"$"
                    x$ = "A"+str(argnum)+"$ = args!.get("+str(argnum)+").getStrVal()"
            fi
            if arg!.getType()=org.dwcj.bbj.BBjVar.BBjGenericType.OBJECT then
                    stmt$=stmt$+",A"+str(argnum)+"!"
                    x$ = "A"+str(argnum)+"! = args!.get("+str(argnum)+").getObjVal()"
            fi
            prep$=prep$+x$+$0a$
        next

        prep$=prep$+stmt$+$0a$

        rem create prep call program

            f! = java.io.File.createTempFile("pgm_",".bbj")
            f!.deleteOnExit()

            ch=unt
            open (ch,mode="O_CREATE,O_TRUNC")f!.getAbsolutePath()
            print (ch)prep$
            close (ch)
            call f!.getAbsolutePath()
            erase f!.getAbsolutePath()

            ret! = new java.util.ArrayList()
            for argnum=0 to args!.size()-1
                arg! = args!.get(argnum)
                if arg!.getType()=org.dwcj.bbj.BBjVar.BBjGenericType.NUMERIC then
                    ret!.add(new org.dwcj.bbj.BBjVar(new java.math.BigDecimal(str(neval("A"+str(argnum))))))
                fi
                if arg!.getType() = org.dwcj.bbj.BBjVar.BBjGenericType.INTEGER then
                        ret!.add(new org.dwcj.bbj.BBjVar(new Integer(neval("A"+str(argnum)+"%"))))
                fi
                if arg!.getType()= org.dwcj.bbj.BBjVar.BBjGenericType.STRING then
                        ret!.add(new org.dwcj.bbj.BBjVar(seval("A"+str(argnum)+"$")))
                fi
                if arg!.getType()=org.dwcj.bbj.BBjVar.BBjGenericType.OBJECT then
                        ret!.add(new org.dwcj.bbj.BBjVar(eval("A"+str(argnum)+"!")))
                fi
            next


        methodret ret!
    methodend

    method public String getWorkingDirectory()
        methodret DIR("")
    methodend

classend
